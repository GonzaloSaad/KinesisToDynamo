buildscript {
    ext {
        springBootVersion = '2.0.3.RELEASE'
        wrapperVersion = '1.0.12.RELEASE'
        shadowVersion = '2.0.3'
    }
    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.springframework.boot.experimental:spring-boot-thin-gradle-plugin:${wrapperVersion}")
        classpath("com.github.jengelman.gradle.plugins:shadow:${shadowVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'org.springframework.boot'


group = 'io.spring.sample'
version = '2.0.0.BUILD-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}

ext {
    springCloudFunctionVersion = "1.0.0.RELEASE"
    springDataDynamoDBVersion = "5.0.2"
    awsLambdaEventsVersion = "2.2.2"
    awsLambdaCoreVersion = "1.1.0"
    awsKinesisAggregatorVersion = "1.0.3"
    awsKinesisJDK = "1.11.368"
}

ext['reactor.version'] = "3.1.7.RELEASE"

assemble.dependsOn = [shadowJar]

jar {
    manifest {
        attributes 'Start-Class': 'example.AppStart'
    }
}

import com.github.jengelman.gradle.plugins.shadow.transformers.*

shadowJar {
    classifier = 'aws'
    dependencies {
        exclude(dependency("org.springframework.cloud:spring-cloud-function-web:${springCloudFunctionVersion}"))
    }
    // Required for Spring
    mergeServiceFiles()
    append 'META-INF/spring.handlers'
    append 'META-INF/spring.schemas'
    append 'META-INF/spring.tooling'
    transform(PropertiesFileTransformer) {
        paths = ['META-INF/spring.factories' ]
        mergeStrategy = "append"
    }
}

configurations {
    testCompile.extendsFrom(compileOnly)
}

dependencies {
    compile("org.springframework.cloud:spring-cloud-starter-function-web:${springCloudFunctionVersion}")
    compile("org.springframework.cloud:spring-cloud-function-adapter-aws:${springCloudFunctionVersion}")
    compile group: 'org.springframework.data', name: 'spring-data-commons', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: springBootVersion
    compile group: 'com.github.derjust', name: 'spring-data-dynamodb', version: '5.0.2'
    compile group: 'com.amazonaws', name: 'aws-lambda-java-events', version: '2.2.2'
    compile group: 'com.amazonaws', name: 'amazon-kinesis-deaggregator', version: '1.0.3'
    compile group: 'com.amazonaws', name: 'amazon-kinesis-aggregator', version: '1.0.3'
    compile group: 'com.amazonaws', name: 'aws-java-sdk-kinesis', version: '1.11.368'
    compileOnly("com.amazonaws:aws-lambda-java-core:${awsLambdaCoreVersion}")
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

